if(WIN32)
  if(MSVC)
    if(CMAKE_SIZEOF_VOID_P EQUAL 8)
      set(lib_dir vc_x64_lib)
    else()
      set(lib_dir vc_lib)
    endif()
  endif()

  if(MINGW)
    if(CMAKE_SIZEOF_VOID_P EQUAL 8)
      set(lib_dir gcc_x64_lib)
    else()
      set(lib_dir gcc_lib)
    endif()
  endif()

  if(CMAKE_BUILD_TYPE STREQUAL "Debug")
    set(mswu_suffix d)
  else()
    set(mswu_suffix "")
  endif()
endif()

set(file_name "wxWidgets-3.3.1")
set(file_url "https://github.com/wxWidgets/wxWidgets/releases/download/v3.3.1/wxWidgets-3.3.1.zip")
wxt_download_zip_file(${file_url} ${file_name})
if(NOT EXISTS ${WXT_3RD_DIR}/${file_name})
  make_directory(${WXT_3RD_DIR}/${file_name})
  execute_process(COMMAND ${CMAKE_COMMAND} -E tar xzf ${WXT_3RD_DIR}/${file_name}.zip
                  WORKING_DIRECTORY ${WXT_3RD_DIR}/${file_name})
endif()

set(WXT_WX_LOCAL_DIR "${WXT_3RD_DIR}/${file_name}/locale")
set(wxWidgets_ROOT_DIR ${WXT_LIBS_DIR}/${WXT_BUILD_FLAG}/${file_name})
if(NOT EXISTS ${wxWidgets_ROOT_DIR})
  include_directories(${WXT_3RD_DIR}/${file_name}/include)
  add_subdirectory(${WXT_3RD_DIR}/${file_name})
  set(wxWidgets_LIBRARIES wx::base wx::aui)
  wxt_install_package(${file_name})
  return()
endif()

set(CMAKE_PREFIX_PATH ${wxWidgets_ROOT_DIR} ${CMAKE_PREFIX_PATH})
find_package(wxWidgets CONFIG REQUIRED)
if(NOT wxWidgets_FOUND)
  message(FATAL_ERROR "[wxTools-wxWidgets] wxWidgets not found")
endif()
